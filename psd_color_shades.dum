#---------------------------------------------------------------------------
# plot_shaded_dots <x> <y> <color-coding-vec> <expand>
plot_shaded_dots 34

	#---------------------------------------------
	# defining color palette
	#
	# if(!$?ncolpalette) {
	   define ncolpalette ? { Number of colors in the palette : }
	# }
	color_map $ncolpalette

	if($?4) { define myexp $4 } else { define myexp 1 }

	vecminmax $3 min_for_col max_for_col

	define min_for_col ? {  Minimum value of color coding vector :  }
	define max_for_col ? {  Maximum value of color coding vector :  }

	#
	# drawing itself 
	#
	echo ----------------------------------------------------------------------
	echo Now select an exponent to be used in the \$value-color conversion.
	echo   eta > 1 : more colors used for the top    of the range of \$value
	echo   eta < 1 :                          bottom 
	define expo ? { Exponent used in color-mapping ? }
	set colv = int( (dimen(coldum)-1)*(($3 - $min_for_col)/($max_for_col - $min_for_col))**$expo )
	echo colv DONE

	lweight 3 
	do 4=0,$(dimen($1)-1) {
	    if( $(colv[$4]) <= $(dimen(coldum)-1) ){
	       ctype $(colv[$4])
	    } else {
	       ctype $(dimen(coldum)-1)
	    }

	    ptype 4 3 expand $(2.0*$myexp) relocate $($1[$4]) $($2[$4]) dot 
	}

	#---------------------------------------------
	# Restoring ColorMap
	#
	delete coldum
        echo Restoring ColorMap
	restore_colors_default

	define border "n"
	define border ? { Draw dot-borders [y/n] : }
	if( substr('$border',0,1) == 'y' ) {
	lweight 1
	do 4=0,$(dimen($1)-1) {
	    ctype 2
	    ptype 4 0 expand $(2.1*$myexp) relocate $($1[$4]) $($2[$4]) dot 
	}
	lweight 3
	}

	#---------------------------------------------
	# Plotting color scale
	#
	# i) defining color palette
	#
	echo Re-Defining color palette
	color_map $ncolpalette $mappa
	echo Drawing color scale 

	# location from plot_dtlc "short(er)": 4500 31500 4500 29500
	# location 29500 31000 5000 29000
	location 4500 31500 30000 31000
	limits $min_for_col $max_for_col 0 1
	# ticksize 0 0 0 0 
	ticksize 0 0 1 1
	expand 1.1
	box 0 0 1 0

	ptype 4 3 
	expand 2.5

	set ydum  = $min_for_col,$max_for_col,$(($max_for_col-$min_for_col)/99)
	set colv2 = int( (dimen(coldum)-1)*((ydum - $min_for_col)/($max_for_col-$min_for_col))**$expo ) 

	do k=0,$(dimen(ydum)-2) {
	    ctype $(colv2[$k])
	    foreach step ( 0.25 0.5 0.75 ) {
	       relocate $(ydum[$k]) $step
	       dot
	    }
	}

	#---------------------------------------------
	# Restoring ColorMap
	#
        echo Restoring ColorMap
	restore_colors_default

	ctype 2 
	expand 1.1
	ticksize 0 0 1 1
	box 0 0 1 0
	expand 2.5

	delete colours
	delete cnames

#---------------------------------------------------------------------------
