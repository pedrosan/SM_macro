#===============================================================================
## source_list_menu
source_list_menu

	# define LATarchive    "/home/gfossati/Science/Fermi/data"
	define LATarchive    "/home/gfossati/Science/Fermi/monitored_objects/data"
	define SMARTSarchive "/home/gfossati/Science/Fermi/smarts/data/new"

	set dimen(source_list) = 22.s
        set source_list= < "  0208-512 " \
                           "  0235+164 " \
                           " (LSI +61 303)" \
                           "  PKS 0528+134 " \
                           " (PKS 0537-441)" \
                           " (0716+714)" \
		           " (0827+243)" \
                           "  OJ 287 " \
                           " (Mrk 421)" \
                           "  3C 273 " \
                           "  3C 279 " \
		           "  1406-076 " \
		           " (PKS 1454-354)" \
                           " (PKS 1502+106)" \
                           "  1510-089 " \
		           "  PKS B 1622-297 " \
                           " (1633+382)" \
                           " (Mrk 501)" \
                           "  1730-130 " \
                           "  PKS 2155-304 " \
                           " (BL Lac)" \
                           "  3C 454.3" >

	if( $?1 ) {
	   define whatsource $1
	   define 9 $whatsource
	} else {
	   do 9=0,dimen(source_list)-1 {
	       echo " ["$9"]  "$(source_list[$9])
	   }
	   define whatsource ? <  What source : >
	   define 9 $whatsource
	}

#-------------------------------------------------------------------------------
# read_smarts_tab_data  [<src sequence number>]
read_smarts_tab_data 01

	define 9 $1

        if( $9 ==  0 ) { define srcNAME "/0208" }
        if( $9 ==  1 ) { define srcNAME "/0235" }
        if( $9 ==  2 ) { echo "DATA NOT AVAILABLE FOR THIS OBJECT" } 
        if( $9 ==  3 ) { define srcNAME "/0528" }
        if( $9 ==  4 ) { echo "DATA NOT AVAILABLE FOR THIS OBJECT" }
        if( $9 ==  5 ) { echo "DATA NOT AVAILABLE FOR THIS OBJECT" }
        if( $9 ==  6 ) { echo "DATA NOT AVAILABLE FOR THIS OBJECT" }
        if( $9 ==  7 ) { define srcNAME "/OJ287" }
        if( $9 ==  8 ) { echo "DATA NOT AVAILABLE FOR THIS OBJECT" }
        if( $9 ==  9 ) { define srcNAME "/3C273" }
        if( $9 == 10 ) { define srcNAME "/3C279" }
        if( $9 == 11 ) { define srcNAME "/1406" }
        if( $9 == 12 ) { echo "DATA NOT AVAILABLE FOR THIS OBJECT" }
        if( $9 == 13 ) { echo "DATA NOT AVAILABLE FOR THIS OBJECT" }
        if( $9 == 14 ) { define srcNAME "/1510" }
        if( $9 == 15 ) { define srcNAME "/1622" }
        if( $9 == 16 ) { echo "DATA NOT AVAILABLE FOR THIS OBJECT" }
        if( $9 == 17 ) { echo "DATA NOT AVAILABLE FOR THIS OBJECT" }
        if( $9 == 18 ) { define srcNAME "/1730" }
        if( $9 == 19 ) { define srcNAME "/2155" }
        if( $9 == 20 ) { echo "DATA NOT AVAILABLE FOR THIS OBJECT" }
        if( $9 == 21 ) { define srcNAME "/3C454" }

	data "$!SMARTSarchive""$!srcNAME""_DCF_B_mag.dat"
	read < mjd_b 2 mag_b 4 emag_b 5 > 
	sort < mjd_b   mag_b   emag_b >
	data "$!SMARTSarchive""$!srcNAME""_DCF_V_mag.dat"
	read < mjd_v 2 mag_v 4 emag_v 5 >
	sort < mjd_v   mag_v   emag_v   >
	data "$!SMARTSarchive""$!srcNAME""_DCF_R_mag.dat"
	read < mjd_r 2 mag_r 4 emag_r 5 >
	sort < mjd_r   mag_r   emag_r   >
	data "$!SMARTSarchive""$!srcNAME""_DCF_J_mag.dat"
	read < mjd_j 2 mag_j 4 emag_j 5 >
	sort < mjd_j   mag_j   emag_j   >
	data "$!SMARTSarchive""$!srcNAME""_DCF_H_mag.dat"
	read < mjd_h 2 mag_h 4 emag_h 5 >
	sort < mjd_h   mag_h   emag_h   >
	data "$!SMARTSarchive""$!srcNAME""_DCF_K_mag.dat"
	read < mjd_k 2 mag_k 4 emag_k 5 > 
	sort < mjd_k   mag_k   emag_k   > 
 
	# ??? Not sure what's this for?
	foreach 8 < b v r j h k > { 
	   set orig_mag_$8 = mag_$8
	   set orig_mjd_$8 = mjd_$8
	}

	# probably unnecessary with new *_cal files
	foreach 8 < b v r j h k > { 
	   set filter = abs(mag_$8)
	   foreach 9 < mjd mag emag > {
	      set $9_$8 = $9_$8   if( filter < 999 )   # changed to match the dummy table values
	   }
	   # set mjd_$8 = mjd_$8 - 2400000.5
	}

        define s0b  (4440) 
	define nu_b (14.833)
        define s0v  (3480) 
	define nu_v (14.732)
        define s0r  (3010) 
	define nu_r (14.632)
        define s0j  (1510) 
	define nu_j (14.382)
        define s0h  (981) 
	define nu_h (14.236)
        define s0k  (614) 
	define nu_k (14.136)

	define f0 ( 1 )
	foreach 8 < b v r j h k > { 
	   define f0 $s0$8
	   set f_$8 = $f0*10.0**(-0.4*mag_$8)
	   set lg_f_$8 = lg(f_$8) + 3.0 
	}

	#---------------------------

#-------------------------------------------------------------------------------
# read_lat_data  [<src sequence number>]
read_lat_data 01

	source_list_menu 

	define 9 $whatsource

        if( $9 ==  0 ) { data "$!LATarchive/0208-512.dat" }
        if( $9 ==  1 ) { data "$!LATarchive/0235+164.dat" }
        if( $9 ==  2 ) { data "$!LATarchive/LSI_+61_303.dat" }
        if( $9 ==  3 ) { data "$!LATarchive/PKS_0528+134.dat" }
        if( $9 ==  4 ) { data "$!LATarchive/PKS_0537-441.dat" }
        if( $9 ==  5 ) { data "$!LATarchive/0716+714.dat" }
        if( $9 ==  6 ) { data "$!LATarchive/0827+243.dat" }
        if( $9 ==  7 ) { data "$!LATarchive/OJ_287.dat" }
        if( $9 ==  8 ) { data "$!LATarchive/Mrk_421.dat" }
        if( $9 ==  9 ) { data "$!LATarchive/3C_273.dat" }
        if( $9 == 10 ) { data "$!LATarchive/3C_279.dat" }
        if( $9 == 11 ) { data "$!LATarchive/1406-076.dat" }
        if( $9 == 12 ) { data "$!LATarchive/PKS_1454-354.dat" }
        if( $9 == 13 ) { data "$!LATarchive/PKS_1502+106.dat" }
        if( $9 == 14 ) { data "$!LATarchive/1510-089.dat" }
        if( $9 == 15 ) { data "$!LATarchive/PKS_B_1622-297.dat" }
        if( $9 == 16 ) { data "$!LATarchive/1633+382.dat" }
        if( $9 == 17 ) { data "$!LATarchive/Mrk_501.dat" }
        if( $9 == 18 ) { data "$!LATarchive/1730-130.dat" }
        if( $9 == 19 ) { data "$!LATarchive/PKS_2155-304.dat" }
        if( $9 == 20 ) { data "$!LATarchive/BL_Lac.dat" }
        if( $9 == 21 ) { data "$!LATarchive/3C_454.3.dat" }
 
	read <  mjd 1 dt 2 fa 3 efa 4 ula 5 fc 6 efc 7 ulc 8 fb 9 efb 10 ulb 11 >

	set dummy_f  = fa*0 + 99
	set dummy_ef = fa*0 + 9999
	foreach 9 < a b c > {
	   set f$9  = (  f$9 > 0 ) ?  f$9 : dummy_f 
	   set ef$9 = ( ef$9 > 0 ) ? ef$9 : dummy_ef 
	}

	define smoothing_width (3)
	foreach 9 < a b c > {
	   #set sn_$9 = f$9/ef$9
	   set sn_$9 = ( ef$9 > 0 ) ? f$9/ef$9 : sn_dummy
	   set sf$9 = myvsmooth(f$9,$smoothing_width)
	}

	set counter = 1,dimen(mjd),1

	set ratio_ab   = fa/fb
	set sratio_ab  = sfa/sfb
	set flag_ul_ab = ula + 2*ulb
	set ratio_ac   = fa/fc
	set sratio_ac  = sfa/sfc
	set flag_ul_ac = ula + 2*ulc
	set ratio_bc   = fb/fc
	set sratio_bc  = sfb/sfc
	set flag_ul_bc = ulb + 2*ulc

	echo "------------------------------------------------------------"
	echo "  The arrays are labeled a/b/c in the following way:"
	echo "    a = 0.1-300 GeV "
	echo "    b = 0.3-1.0 GeV "
	echo "    c = 1.0-300 GeV "
	echo " "
	echo "  Array names are : " 
	echo "     f#   ef#   ul#   sn_#   sf#  "
	echo "------------------------------------------------------------"

#-------------------------------------------------------------------------------
# plot_lat_lc
plot_lat_lc

	reset_graph
	location 5500 31000  8500 29000

	read_lat_data 

	#---------------------------------------
	# Preparing data arrays
	#---------------------------------------
	# daily
	foreach 9 < mjd dt fa efa > { 
	   set dgood_$9 = $9   if( ula == 0  && dt < 1.0 )
	   set dul_$9   = $9   if( ula == 1  && dt < 1.0 )
	}
	# weekly
	foreach 9 < mjd dt fa efa > { 
	   set wgood_$9 = $9   if( ula == 0  && dt > 1.0 )
	   set wul_$9   = $9   if( ula == 1  && dt > 1.0 )
	}

	set dmjd = dgood_mjd CONCAT dul_mjd
	sort < dmjd >
	define tmin $(dmjd[0])
	define tmax $(dmjd[dimen(dmjd)-1])

	echo " Data arrays Time range : "$tmin "-" $tmax
	echo " Calling jd.x to check today\'s date "
	systemcall jd.x | egrep mjd | tee /tmp/today_mjd
	data "/tmp/today_mjd"
	define today_mjd read 1 3 
	echo $today_mjd

	# define tmin $(dmjd[0] - 15)
	# define tmax $(dmjd[dimen(dmjd)-1] + 15)
	define tmin $(54688 - 15)
	define tmax $(int($today_mjd) + 15 )

	define tmin ? < Plot start time : >
	define tmax ? < Plot end   time : >
	#---------------------------------------
	
	range 0 3 
	# limits $tmin $tmax $(lg(1e-8)) $(lg(7e-6))
	limits $tmin $tmax (lg(dgood_fa))
	notation -5 5 -4 4 
	ticksize 0 0 -1 0 

	#---------------------------------------
	window 1 -2 1 1
	box 1 2 0 0 
	xlabel  Time [MJD]

	#---------------------------------------
	# daily averages
	#---------------------------------------
	ptype 4 3 
	expand 0.5
	ctype 2 
	points dul_mjd (lg(dul_fa))
	ctype 3 
	expand 1.25
	points dgood_mjd (lg(dgood_fa))   
	expand 0.5
	logerr dgood_mjd (lg(dgood_fa)) dgood_efa

	ctype 2 
	expand 1.5

	#---------------------------------------
	window 1 -2 1 2
	box 0 2 0 0 

	#---------------------------------------
	# weekly averages
	#---------------------------------------
	ptype 4 3 
	expand 0.5
	ctype 6 
	points wul_mjd (lg(wul_fa))
	ctype 5 
	expand 1.25
	points wgood_mjd (lg(wgood_fa))   
	expand 0.5
	logerr wgood_mjd (lg(wgood_fa)) wgood_efa

	ctype 5 
	# histogram wgood_mjd (lg(wgood_fa))

	ctype 2
	expand 1.1
	define str_top ? < Top label string : > 
	relocate ( $gx1 $($gy2+750) )
	putlabel 6 $str_top

	ctype 2 
	expand 1.5
	window 1 1 1 1
	ylabel  Flux 0.1-300 GeV [c.g.s.]

#-------------------------------------------------------------------------------
# plot_lat_lc_with_hr  
#      the plotted ratio is 0.3-1/1-300 GeV, more like a softness ratio!
plot_lat_lc_with_hr

	reset_graph
	location 5500 31000  8500 29000

	read_lat_data 

	#---------------------------------------
	# Preparing data arrays
	#---------------------------------------
	# daily
	foreach 9 < mjd dt fa efa sfa fb efb sfb fc efc sfc > { 
	   set dgood_f_$9 = $9   if( ula == 0  && dt < 1.0 )
	   set dul_f_$9   = $9   if( ula == 1  && dt < 1.0 )
	}
	foreach 9 < mjd dt fa efa fb efb fc efc > { 
	   set wgood_f_$9 = $9   if( ula == 0  && dt > 1.0 )
	   set wul_f_$9   = $9   if( ula == 1  && dt > 1.0 )
	}

	# hr
	foreach 9 < mjd dt ratio_bc sratio_bc > { 
	   set dgood_r_$9 = $9   if( ula == 0  && dt < 1.0 )
	   set dul_r_$9   = $9   if( ula == 1  && dt < 1.0 )
	}
	foreach 9 < mjd dt ratio_bc sratio_bc > { 
	   set wgood_r_$9 = $9   if( ula == 0  && dt > 1.0 )
	   set wul_r_$9   = $9   if( ula == 1  && dt > 1.0 )
	}
	set vgood_ratio_bc = ratio_bc  if( dt> 1 && sn_b >= 3 && sn_c >=3 && ulb == 0 && ulc == 0 )
	set vgood_mjd      = mjd       if( dt> 1 && sn_b >= 3 && sn_c >=3 && ulb == 0 && ulc == 0 )

	set dmjd = dgood_f_mjd CONCAT dul_f_mjd
	sort < dmjd >
	define tmin $(dmjd[0])
	define tmax $(dmjd[dimen(dmjd)-1])

	echo " Data arrays Time range : "$tmin "-" $tmax
	echo " calling jd.x to check today\'s date "
	systemcall jd.x | egrep mjd | tee /tmp/today_mjd
	data "/tmp/today_mjd"
	define today_mjd read 1 3 
	echo $today_mjd

	# define tmin $(dmjd[0] - 15)
	# define tmax $(dmjd[dimen(dmjd)-1] + 15)
	define tmin $(54688 - 15)
	define tmax $(int($today_mjd) + 15 )

	define tmin ? < Plot start time : >
	define tmax ? < Plot end   time : >
	#---------------------------------------
	
	notation -5 5 -4 4 

	#=======================================
	# l.c.: bottom window
	#---------------------------------------
	window 1 -2 1 1
	range 0 3
	# limits $tmin $tmax $(lg(1e-8)) $(lg(7e-6))
	limits $tmin $tmax (lg(dgood_f_fa))
	ticksize 0 0 -1 0 
	box 1 2 0 0 
	xlabel  Time [MJD]
	ylabel  F_{0.1-300 GeV} [c.g.s.]

	#---------------------------------------
	# daily averages
	#---------------------------------------
	ptype 4 3 
	expand 0.5
	ctype 2 
	points dul_f_mjd (lg(dul_f_fa))
	ctype 3 
	expand 1.25
	points dgood_f_mjd (lg(dgood_f_fa))   
	expand 0.5
	logerr dgood_f_mjd (lg(dgood_f_fa)) dgood_f_efa
	ctype 5 
	expand 0.90
	points wgood_f_mjd (lg(wgood_f_fa))   

	ctype 2 
	expand 1.5

	#=======================================
	# HR: top window
	#---------------------------------------
	range 0 0 
	window 1 -2 1 2
	limits $tmin $tmax wgood_r_ratio_bc
	limits $tmin $tmax -0.1 7.6
	limits $tmin $tmax -0.1 11.1
	limits $tmin $tmax 11.1 -0.1
	ticksize 0 0 0 0 
	box 0 2 0 0 
	ylabel F_{0.3-1 GeV}/F_{1-300 GeV}

	ptype 4 3 
	expand 0.5
	ctype 6 
	points wul_r_mjd wul_r_ratio_bc
	 ctype 3 
	 expand 1.25
	 points dgood_r_mjd dgood_r_sratio_bc
	ctype 5 
	expand 1.25
	points wgood_r_mjd wgood_r_ratio_bc
	# expand 0.5
	# logerr good_mjd (lg(good_fa)) good_efa
	ctype 4 
	ptype 4 0 
	expand 1.5
	points vgood_mjd vgood_ratio_bc

	ctype 5 
	# histogram wgood_mjd (lg(wgood_fa))

	ctype 2
	expand 1.1
	define str_top ? < Top label string : > 
	relocate ( $gx1 $($gy2+750) )
	putlabel 6 $str_top

	ctype 2 
	expand 1.5
	window 1 1 1 1
	# ylabel  Flux 0.1-300 GeV [c.g.s.]

#-------------------------------------------------------------------------------
# hr_vs_flux
#     ad hoc for 3C454 (maybe)
#     Need to run after 'plot_lat_lc_with_hr' to have the arrays properly setup
hr_vs_flux

	reset_graph

	limits -6.5 -4.4 7.5 0.5
	ticksize -1 0 0 0 
	box
	xlabel  F_{0.1-300 GeV} [c.g.s.]
	ylabel F_{0.3-1 GeV}/F_{1-300 GeV}

	echo "------------------------------"
	echo " Options for plotting : "
	echo "  [1] smoothed daily  data "
	echo "  [2]          daily  data "
	echo "  [3]          weekly data "
	define flag_what ? < What to plot : >
	if( $flag_what == 1 ) { 
	   ctype 2 
	   points (lg(dgood_f_sfa)) dgood_r_sratio_bc
	   ctype 5
	   points (lg(dgood_f_sfa)) dgood_r_sratio_bc  if( dgood_f_mjd < 54800 )
	   ctype 3
	   points (lg(dgood_f_sfa)) dgood_r_sratio_bc  if( dgood_f_mjd > 55050 )
	}
	if( $flag_what == 2 ) { 
           ctype 2 
           points (lg(dgood_f_fa)) dgood_r_ratio_bc
           ctype 5
           points (lg(dgood_f_fa)) dgood_r_ratio_bc  if( dgood_f_mjd < 54800 )
           ctype 3
           points (lg(dgood_f_fa)) dgood_r_ratio_bc  if( dgood_f_mjd > 55050 )
	}
	if( $flag_what == 3 ) { 
           ctype 2 
           points (lg(wgood_f_fa)) wgood_r_ratio_bc
           ctype 5
           points (lg(wgood_f_fa)) wgood_r_ratio_bc  if( wgood_f_mjd < 54800 )
           ctype 3
           points (lg(wgood_f_fa)) wgood_r_ratio_bc  if( wgood_f_mjd > 55050 )
	}
	ctype 2 

#-------------------------------------------------------------------------------
# plot_lat_and_smarts
plot_lat_and_smarts

	source_list_menu
	define 9 $whatsource

	read_smarts_tab_data $9
	read_lat_data        $9

	reset_graph
	location 5500 31000  8500 29000

	define whatsmarts ? < Which SMARTS band [b|r|v|j|h|k] : >
	define 7 $whatsmarts

	#---------------------------------------
	# Preparing data arrays
	#---------------------------------------
	# daily
	foreach 9 < mjd dt fa efa sfa > { 
	   set dgood_$9 = $9   if( ula == 0  && dt < 1.0 )
	   set dul_$9   = $9   if( ula == 1  && dt < 1.0 )
	}

	set dmjd = dgood_mjd CONCAT dul_mjd
	sort < dmjd >
	define tmin $(dmjd[0])
	define tmax $(dmjd[dimen(dmjd)-1])

	echo " Data arrays Time range : "$tmin "-" $tmax
	echo " calling jd.x to check today\'s date "
	systemcall jd.x | egrep mjd | tee /tmp/today_mjd
	data "/tmp/today_mjd"
	define today_mjd read 1 3 
	echo $today_mjd

	# define tmin $(dmjd[0] - 15)
	# define tmax $(dmjd[dimen(dmjd)-1] + 15)
	define tmin $(54688 - 15)
	define tmax $(int($today_mjd) + 15 )

	define tmin ? < Plot start time : >
	define tmax ? < Plot end   time : >

	range 0 2 
	range 0 3 

	#---------------------------------------
	# Fermi/LAT daily averages
	#---------------------------------------
	limits $tmin $tmax $(lg(1e-8)) $(lg(7e-6))
	limits $tmin $tmax (lg(dgood_fa))
	notation -5 5 -4 4 
	ticksize 0 0 -1 0 

	#---------------------------------------
	window 1 -2 1 1
	box 1 2 0 0 
	xlabel Time [MJD]
	ylabel F_{LAT} [c.g.s.]

	ptype 4 3 
	expand 0.5
	ctype 2 
	ctype grey60
	points dul_mjd (lg(dul_fa))
	ctype 3 
	expand 1.25
	points dgood_mjd (lg(dgood_fa))   
	expand 0.5
	logerr dgood_mjd (lg(dgood_fa)) dgood_efa

	ctype 2 
	expand 1.5

	#---------------------------------------
	# smarts 
	#---------------------------------------
	window 1 -2 1 2
	limits $tmin $tmax lg_f_$7
	box 0 2 0 0 
	ylabel F_$7 [mJy]

	ptype 4 3 
	expand 0.5
	ctype 5 
	expand 1.25
	points mjd_$7 lg_f_$7

	ctype 5 
	# histogram wgood_mjd (lg(wgood_fa))

	ctype 2
	expand 1.1
	define str_top ? < Top label string : > 
	relocate ( $gx1 $($gy2+750) )
	putlabel 6 $str_top

	ctype 2 
	expand 1.5
	window 1 1 1 1

#-------------------------------------------------------------------------------
# plot_lat_and_smarts_1box
plot_lat_and_smarts_1box

	reset_graph
	location 5500 29000  8500 29000

	source_list_menu
	define 9 $whatsource

	read_smarts_tab_data $9
	read_lat_data        $9

	define whatsmarts ? < Which SMARTS band [b|r|v|j] : >
	define 7 $whatsmarts

	#---------------------------------------
	# Preparing data arrays
	#---------------------------------------
	# daily LAT
	foreach 9 < mjd dt fa efa > { 
	   set dgood_$9 = $9   if( ula == 0  && dt < 1.0 )
	   set dul_$9   = $9   if( ula == 1  && dt < 1.0 )
	}

	set dmjd = dgood_mjd CONCAT dul_mjd
	sort < dmjd >
	define tmin $(dmjd[0])
	define tmax $(dmjd[dimen(dmjd)-1])

	echo " Data arrays Time range : "$tmin "-" $tmax
	echo " calling jd.x to check today\'s date "
	systemcall jd.x | egrep mjd | tee /tmp/today_mjd
	data "/tmp/today_mjd"
	define today_mjd read 1 3 
	echo $today_mjd

	# define tmin $(dmjd[0] - 15)
	# define tmax $(dmjd[dimen(dmjd)-1] + 15)
	define tmin $(54688 - 15)
	define tmax $(int($today_mjd) + 15 )

	define tmin ? < Plot start time : >
	define tmax ? < Plot end   time : >

	#---------------------------------------
	# Fermi/LAT daily averages
	#---------------------------------------
	range 0 2 
	range 0 3 
	limits $tmin $tmax $(lg(1e-8)) $(lg(7e-6))
	limits $tmin $tmax (lg(dgood_fa))
	notation -5 5 -4 4 
	ticksize 0 0 -1 0 

	#---------------------------------------
	ctype 3 
	box 0 2 0 4
	ylabel F_{LAT} [c.g.s.]
	ctype 2 
	box 1 4 0 4
	xlabel Time [MJD]

	ptype 4 3 
	expand 0.5
	ctype 2 
	points dul_mjd (lg(dul_fa))
	ctype 3 
	expand 1.25
	points dgood_mjd (lg(dgood_fa))   
	expand 0.5
	logerr dgood_mjd (lg(dgood_fa)) dgood_efa

	ctype 2 
	expand 1.5

	#---------------------------------------
	# smarts 
	#---------------------------------------
	limits $tmin $tmax lg_f_$7
	#box 0 4 0 4 
	ctype 5
	box 4 4 4 2 
	# ylabel F_$7 [arb.]

	ptype 4 3 
	expand 0.5
	ctype 5 
	expand 1.25
	points mjd_$7 lg_f_$7

	ctype 5 
	# histogram wgood_mjd (lg(wgood_fa))

	ctype 2
	expand 1.1
	define str_top ? < Top label string : > 
	relocate ( $gx1 $($gy2+750) )
	putlabel 6 $str_top

	ctype 2 
	expand 1.5
	window 1 1 1 1

#-------------------------------------------------------------------------------
# plot_2smarts_1box
plot_2smarts_1box

	reset_graph
	location 5500 29000  8500 29000

	define whatsmarts1 ? < Which SMARTS band [b|r|v|j] : >
	define whatsmarts2 ? < Which SMARTS band [b|r|v|j] : >
	define 7 $whatsmarts1
	define 8 $whatsmarts2

	#---------------------------------------
	# daily LAT (used for reference for time interval)
	foreach 9 < mjd dt fa efa > { 
	   set dgood_$9 = $9   if( ula == 0  && dt < 1.0 )
	   set dul_$9   = $9   if( ula == 1  && dt < 1.0 )
	}

	set dmjd = dgood_mjd CONCAT dul_mjd
	sort < dmjd >
	define tmin $(dmjd[0])
	define tmax $(dmjd[dimen(dmjd)-1])

	echo " LAT Data arrays Time range : "$tmin "-" $tmax
	echo " calling jd.x to check today\'s date "
	systemcall jd.x | egrep mjd | tee /tmp/today_mjd
	data "/tmp/today_mjd"
	define today_mjd read 1 3 
	echo $today_mjd

	# define tmin $(dmjd[0] - 15)
	# define tmax $(dmjd[dimen(dmjd)-1] + 15)
	define tmin $(54688 - 15)
	define tmax $(int($today_mjd) + 15 )

	define tmin ? < Plot start time : >
	define tmax ? < Plot end   time : >

	range 0 2 
	range 0 3 
	notation -5 5 -4 4 
	ticksize 0 0 -1 0 

	#---------------------------------------
	# SMARTS band 1
	#---------------------------------------
	limits $tmin $tmax lg_f_$7
	ctype 3 
	box 0 2 0 4
	ylabel F_$7 [arb.]
	ctype 2 
	box 1 4 0 4
	xlabel Time [MJD]

	ptype 4 3 
	expand 0.5
	ctype 3 
	expand 1.25
	points mjd_$7 lg_f_$7

	ctype 2 
	expand 1.5

	#---------------------------------------
	# SMARTS band 2
	#---------------------------------------
	limits $tmin $tmax lg_f_$8
	ctype 5
	box 4 4 4 2 
	# ylabel f_$7 [arb.]

	ptype 4 3 
	expand 0.5
	ctype 5 
	expand 1.25
	points mjd_$8 lg_f_$8

	#---------------------------------------
	# top label
	#---------------------------------------
	ctype 2
	expand 1.1
	define str_top ? < Top label string : > 
	relocate ( $gx1 $($gy2+750) )
	putlabel 6 $str_top

	ctype 2 
	expand 1.5
	window 1 1 1 1

#-------------------------------------------------------------------------------
# plot_smarts_vs_smarts  [IN PROGRESS]
plot_smarts_vs_smarts

	reset_graph
	location 5500 29000  8500 29000

	define whatsmarts1 ? < Which SMARTS band [b|r|v|j] : >
	define whatsmarts2 ? < Which SMARTS band [b|r|v|j] : >
	define 7 $whatsmarts1
	define 8 $whatsmarts2

	#---------------------------------------
	# daily LAT (used for reference for time interval)
	foreach 9 < mjd dt fa efa > { 
	   set dgood_$9 = $9   if( ula == 0  && dt < 1.0 )
	   set dul_$9   = $9   if( ula == 1  && dt < 1.0 )
	}

	set dmjd = dgood_mjd CONCAT dul_mjd
	sort < dmjd >
	define tmin $(dmjd[0])
	define tmax $(dmjd[dimen(dmjd)-1])

	echo " LAT Data arrays Time range : "$tmin "-" $tmax
	echo " calling jd.x to check today\'s date "
	systemcall jd.x | egrep mjd | tee /tmp/today_mjd
	data "/tmp/today_mjd"
	define today_mjd read 1 3 
	echo $today_mjd

	# define tmin $(dmjd[0] - 15)
	# define tmax $(dmjd[dimen(dmjd)-1] + 15)
	define tmin $(54688 - 15)
	define tmax $(int($today_mjd) + 15 )

	define tmin ? < Plot start time : >
	define tmax ? < Plot end   time : >

	range 0 3 
	range 0 3 
	notation -5 5 -5 5
	ticksize 0 0 -1 0 

	#---------------------------------------
	# SMARTS band 1
	#---------------------------------------
	limits lg_f_$7  lg_f_$8
	ticksize 0 0 0 0 
	ctype 2 
	box 1 2 0 0 

	xlabel F_$7 [arb.]
	ylabel F_$8 [arb.]

	ptype 4 3 
	expand 0.5
	ctype 3 
	expand 1.25
	points lg_f_$7

	ctype 2 
	expand 1.5

	#---------------------------------------
	# SMARTS band 2
	#---------------------------------------
	limits $tmin $tmax lg_f_$8
	ctype 5
	box 4 4 4 2 
	# ylabel f_$7 [arb.]

	ptype 4 3 
	expand 0.5
	ctype 5 
	expand 1.25
	points mjd_$8 lg_f_$8

	#---------------------------------------
	# top label
	#---------------------------------------
	ctype 2
	expand 1.1
	define str_top ? < Top label string : > 
	relocate ( $gx1 $($gy2+750) )
	putlabel 6 $str_top

	ctype 2 
	expand 1.5
	window 1 1 1 1

#-------------------------------------------------------------------------------
# plot_many  [*** GOOD ONE ***]
#    Plots a set of SMARTS filters, AND Fermi/LAT light curves.
plot_many 

	source_list_menu
	define 9 $whatsource

	read_smarts_tab_data $9
	read_lat_data        $9

	define whatsmarts ? < Which SMARTS band [a string e.g. "b v j k"] : >
	# define 8 $whatsmarts
	set whatsmarts = < $!whatsmarts >

	reset_graph
	location 8000 27000  5500 31000

	#---------------------------------------
	# Preparing data arrays
	#---------------------------------------
	# daily Fermi/LAT
	foreach 9 < mjd dt fa efa > { 
	   set dgood_$9 = $9   if( ula == 0  && dt < 1.0 )
	   set dul_$9   = $9   if( ula == 1  && dt < 1.0 )
	}

	set dmjd = dgood_mjd CONCAT dul_mjd
	help dmjd
	sort < dmjd >
	define tmin $(dmjd[0])
	define tmax $(dmjd[dimen(dmjd)-1])

	echo " Data arrays Time range : "$tmin "-" $tmax
	echo " calling jd.x to check today\'s date "
	systemcall jd.x | egrep mjd | tee /tmp/today_mjd
	data "/tmp/today_mjd"
	define today_mjd read 1 3 
	echo $today_mjd

	#define tmin $(54688 - 15)
	#define tmax $(int($today_mjd) + 15 )

	define tmin (54688)
	define tmax $(int($today_mjd))

	define tmin ? < Plot start time : >
	define tmax ? < Plot end   time : >

	define tfrac (0.05)
	define dttot $( $tmax - $tmin )
	# echo $dttot
	define tmin $($tmin - $tfrac*$dttot)
	define tmax $($tmax + $tfrac*$dttot )
	#---------------------------------------

	#---------------------------------------
	# Time filtering data arrays
	#---------------------------------------
	# daily Fermi/LAT
	set lat_time = dgood_mjd
	foreach 9 < mjd dt fa efa > { 
	   set dgood_$9 = dgood_$9   if( lat_time >= $tmin && lat_time <= $tmax )
	   set dul_$9   = dul_$9     if( lat_time >= $tmin && lat_time <= $tmax )
	}
	delete lat_time
	# smarts

	foreach 7 whatsmarts {
	   set smart_time_$7 = mjd_$7
           set mjd_$7  = mjd_$7   if( smart_time_$7 >= $tmin && smart_time_$7 <= $tmax )
           set lg_f_$7 = lg_f_$7  if( smart_time_$7 >= $tmin && smart_time_$7 <= $tmax )
	   delete smart_time_$7
	}
	#---------------------------------------

	range 0 2 
	range 0 3 

	define nyboxes $(-1*(dimen(whatsmarts)+1))

	#---------------------------------------
	# Fermi/LAT daily averages
	#---------------------------------------
	limits $tmin $tmax $(lg(1e-8)) $(lg(7e-6))
	limits $tmin $tmax (lg(dgood_fa))
	notation -5 5 -1 1 
	# ticksize 0 0 -1 0 
	ticksize 0 0 0 0
	ticksize 0 0 0.25 1 

	#---------------------------------------
	# window 1 -5 1 1
	window 1 $nyboxes 1 1
	expand 1.25
	box 1 2 0 0 
	xlabel Time [MJD]
	ylabel LAT

	ptype 4 3 
	expand 0.5
	ctype 2 
	ctype grey60
	points dul_mjd (lg(dul_fa))
	ctype 3 
	expand 1.25
	points dgood_mjd (lg(dgood_fa))   
	expand 0.5
	logerr dgood_mjd (lg(dgood_fa)) dgood_efa

	ctype 2 
	expand 1.5

	#---------------------------------------
	# smarts 
	#---------------------------------------
	define j (1)
	foreach 7 whatsmarts {
	   define j $($j+1)
	   window 1 $nyboxes 1 $j
	   limits $tmin $tmax lg_f_$7
	   ctype 2 
	   expand 1.25
	   box 0 2 0 0 
	   if( substr('$7',0,1) == 'a' ) { 
	      define ylab "\alpha($(substr('$7',1,0)))"
	   } else {
	      define ylab $7
	   } 
	   ylabel $ylab

           ptype 4 3 
           expand 0.5
           ctype 5 
	   if( substr('$7',0,1) == 'a' ) { ctype orange } 
           expand 1.25
           points mjd_$7 lg_f_$7
	}
	#ctype 5 
	# histogram wgood_mjd (lg(wgood_fa))

        # ctype 2
        # expand 1.1
        # define str_top ? < Top label string : > 
        # relocate ( $gx1 $($gy2+750) )
        # putlabel 6 $str_top

	ctype 2 
	expand 1.5
	window 1 1 1 1
	relocate ( $( 0.5*($gx1+$gx2) ) $( $gy2 + 750) )
	putlabel 5 $(source_list[$whatsource])

#-------------------------------------------------------------------------------
# match_2lc
match_2lc

	source_list_menu
	define 9 $whatsource

	read_smarts_tab_data $9

	define band1 ? < First  OIR band [b|v|r|j|h|k] : >
	define band2 ? < Second OIR band [b|v|r|j|h|k] : >

	define 1 $band1
	define 2 $band2
	define 3 $1$2
	echo $3
	#define eps (0.005)
	define eps_min ? < Threshold [minutes] : > 
	define eps $($eps_min/60/24)

	set t1   = mjd_$1
	set lgf1 = lg_f_$1
	set t2   = mjd_$2
	set lgf2 = lg_f_$2

	set t1x   = t1
	set lgf1x = t1*0 - 50
	set t2x   = t1
	set lgf2x = t1*0 - 50

	define i (0)
	do 7=0,dimen(t1)-1 {
	   define nmatch (0)
	   define smallest_dt (100)
	   do 8=$i,dimen(t2)-1 {
	      define dt $( abs(t1[$7] - t2[$8]) )
	      if( $dt < $smallest_dt ) {
	         define smallest_dt $dt
	      } 
	      if( $dt <= $eps ) { 
		 define nmatch $( $nmatch + 1 )
	         # echo " Match for "$7" with "$8" : "$(t1[$7]) $(t2[$8]) $dt 
		 define i $($8+1)
		 set lgf1x[$7] = lgf1[$7]
		 set lgf2x[$7] = lgf2[$8]
	      }
	   }
	   define tt $( sprintf('%11.5f',$(t1[$7])) )
	   #echo " N_matches["$7"] "$(t1[$7])" = "$nmatch"   smallest dt = "$smallest_dt
	   echo " N_matches["$(sprintf('%4d',$7))"] "$( sprintf('%11.5f',$(t1[$7])) )" = "$nmatch"   smallest dt = "$( sprintf('%10.3f',$(60*24*$smallest_dt)))" minutes"
	   if( $nmatch > 1 ) { 
	      echo " WARNING more than one match "
	   }
	}

	set filter = lgf1x 
	set mjd_$1x$2  = t1x     if( filter > -50 )
	set lg_f_$1x$2 = lgf1x   if( filter > -50 )
	set mjd_$2x$1  = t2x     if( filter > -50 )
	set lg_f_$2x$1 = lgf2x   if( filter > -50 )

	set mjd_$22$1  = mjd_$2x$1
	set lg_f_$22$1 = lg_f_$2x$1 - lg_f_$1x$2
	set mjd_a$2$1  = mjd_$2x$1
	set lg_f_a$2$1 = (lg_f_$2x$1 - lg_f_$1x$2)/($nu_$1 - $nu_$2)

	echo "----------------------------------------------------------------------"
	echo "  Input light curves : "
	echo "    Band "$1" with "$(dimen(lg_f_$1))" data points "
	echo "    Band "$2" with "$(dimen(lg_f_$2))" data points "
	echo "  "
	echo "  Matching threshold is "$eps" days, or "$($eps*24*60)" min. or "$($eps*86400)" seconds"
	echo "  Output light curves have "$(dimen(lg_f_$1x$2))" and "$(dimen(lg_f_$1x$2))" data points"
	echo "----------------------------------------------------------------------"
	echo "  Generated matching light curves with names : "
	echo "     mjd_$1x$2  lg_f_$1x$2 : band 1 matched to band 2 data"
	echo "     mjd_$2x$1  lg_f_$2x$1 : band 2 matched to band 1 data "
	echo "     mjd_$22$1  lg_f_$22$1 (ratio of $2/$1 fluxes)"
	echo "     mjd_a$2$1  lg_f_a$2$1 (spectral index between $2 and $1 bands)"
	echo "----------------------------------------------------------------------"

#-------------------------------------------------------------------------------
# plot_a_br  [BROKEN] ( radio flux vs. \alpha_{B,radio} )
plot_a_br

	reset_graph

	limits (lg_f_r+3) 0 2  
	limits $(lg(0.2))  $(lg(20)) 0 1.75
	ticksize -1 0 0 0 
	box
	xlabel F_R [mJy]
	ylabel \alpha_{BR}

	ctype 3 
	points (lg_orig_f_r + 3) a_br
	ctype 2 

#-------------------------------------------------------------------------------
# check_lat_sn
check_lat_sn

	read_lat_data

	define wx    ? < What X-variable [counter|time] : >
	define wband ? <             Which band [a|b|c] : >
	define 9 $wband

	if( substr('$wx',0,1) == 'c' ) {
	   set xx = counter
	} else {
	   set xx = mjd
	}

	define max_sn (30)

	set rejected = sn_$9   if( sn_$9 >= $max_sn )
	define rejectedpct $(sprintf('%4.1f',$(100.*dimen(rejected)/dimen(mjd))))

	echo " Data non plotted with s/n max = "$max_sn"   :  " $(dimen(rejected)) "[" $rejectedpct "% ]"

	notation -4 5 -4 4 

	ticksize 0 0 1 3 
	limits xx sn_$9
	limits xx -1.5 $max_sn
	box

	ptype 4 3 
	ctype 2 points xx sn_$9
	ctype 3 points xx sn_$9  if( ul$9 >= 1 )
	ctype 4 points xx sn_$9  if( ul$9 <  1 )
	ctype 2

#-------------------------------------------------------------------------------
# check_lat_ratios
check_lat_ratios

	read_lat_data

	define wx    ? < What X-variable [counter|time|flux] : >
	define wband ? <               Which band [ab|ac|bc] : >
	define 9 $wband

	if( substr('$wx',0,1) == 'c' ) {
	   set xx = counter
	   define labx "order #"
	}
	if( substr('$wx',0,1) == 't' ) {
	   set xx = mjd
	   define labx "MJD"
	} 
	if( substr('$wx',0,1) == 'f' ) {
	   set xx = lg(fa)
	   define labx "F_a"
	}

	define max_ff_ab (12)
	define max_ff_ac (45)
	define max_ff_bc (12)
	define max_ff $max_ff_$9

	#--------------------
	define 7 $(substr('$wband',0,1))
	define 8 $(substr('$wband',1,1))

	compute_ratio_ab $7 $8 2.0 r_20
	compute_ratio_ab $7 $8 2.5 r_25
	compute_ratio_ab $7 $8 3.0 r_30
	echo $r_20
	echo $r_25
	echo $r_30
	#--------------------

	vecminmax ratio_$9 m1 m2
	echo " Array min and max : "$m1 $m2

	set rejected = ratio_$9   if( ratio_$9 >= $max_ff )
	if( dimen(rejected) > 0 ) { 
	   define rejectedpct $(sprintf('%4.1f',$(100.*dimen(rejected)/dimen(mjd))))
	   echo " Data non plotted with f/f max = "$max_ff"   :  " $(dimen(rejected)) "[" $rejectedpct "% ]"
	} else {
	   echo " No data rejected with f/f max = "$max_ff
	}

	set temp = ratio_$9   if( flag_ul_$9 == 0 )
	echo " REAL Data points (w/out upper limits) : "$(dimen(temp)) "[" $(sprintf('%4.1f',$(100.*dimen(temp)/dimen(mjd)))) "% ]" 

	notation -4 5 -4 4 

	ticksize 0 0 1 3 
	limits xx ratio_$9
	limits xx -0.5 $max_ff
	limits xx $max_ff -0.5
	box
	xlabel $labx
	ylabel F_$7/F_$8 

	ptype 4 1 expand 1.25 ctype 2 points xx ratio_$9  if( flag_ul_$9 == 3 )
	ptype 4 3 expand 0.75 ctype 3 points xx ratio_$9  if( flag_ul_$9 == 2 )
	ptype 4 3 expand 0.75 ctype 6 points xx ratio_$9  if( flag_ul_$9 == 1 )
	ptype 4 3 expand 1.75 ctype 4 points xx ratio_$9  if( flag_ul_$9 <  1 )
	ctype 2

	ctype 7 
	ltype 2
	relocate $fx1 $r_20 draw $fx2 $r_20
	relocate $fx1 $r_25 draw $fx2 $r_25
	relocate $fx1 $r_30 draw $fx2 $r_30
	ctype 2 
	ltype 0

	echo "---------------------------------------------"
	echo " black crosses :  upper limit / upper limit  ===>  n/a "
	echo " red   squares :         GOOD / upper limit  ===> LOWER limit "
	echo " cyan  squares :  upper limit / GOOD         ===> UPPER limit  "
	echo " green squares :         GOOD / GOOD         ===>  GOOD"
	echo "---------------------------------------------"

	relocate ( $(0.5*($gx1+$gx2)) $($gy2 + 750) )
	putlabel 6 $9

#-------------------------------------------------------------------------------
# histo_lat_ratios
histo_lat_ratios

	read_lat_data

	#define wx    ? < What X-variable [counter|time|flux] : >
	define wband ? <               Which band [ab|ac|bc] : >
	define 9 $wband

	define max_ff_ab (12)
	define max_ff_ac (45)
	define max_ff_bc (12)
	define max_ff $max_ff_$9

	set xx=-0.5,$max_ff_$9+1,1

	#--------------------
	define 7 $(substr('$wband',0,1))
	define 8 $(substr('$wband',1,1))

	compute_ratio_ab $7 $8 2.0 r_20
	compute_ratio_ab $7 $8 2.5 r_25
	compute_ratio_ab $7 $8 3.0 r_30
	echo $r_20
	echo $r_25
	echo $r_30
	#--------------------

	vecminmax ratio_$9 m1 m2
	echo " Array min and max : "$m1 $m2

	set rejected = ratio_$9   if( ratio_$9 >= $max_ff )
	if( dimen(rejected) > 0 ) { 
	   define rejectedpct $(sprintf('%4.1f',$(100.*dimen(rejected)/dimen(mjd))))
	   echo " Data non plotted with f/f max = "$max_ff"   :  " $(dimen(rejected)) "[" $rejectedpct "% ]"
	} else {
	   echo " No data rejected with f/f max = "$max_ff
	}

	set temp = ratio_$9   if( flag_ul_$9 == 0 )
	echo " REAL Data points (w/out upper limits) : "$(dimen(temp)) "[" $(sprintf('%4.1f',$(100.*dimen(temp)/dimen(mjd)))) "% ]" 

	set ratio_good    = ratio_$9   if( flag_ul_$9 <  1 )
	set ratio_ll      = ratio_$9   if( flag_ul_$9 == 2 )
	set ratio_with_ll = ratio_$9   if( flag_ul_$9 < 1 || flag_ul_$9 == 2 )

	set hratio_with_ll = histogram(ratio_with_ll:xx)
	set hratio_good    = histogram(ratio_good:xx)

	ticksize 0 0 0 0
	limits xx hratio_with_ll
	box
	xlabel F_$7/F_$8 
	ylabel N

	define shift local
	define shift (0.025)
	ctype 3 histogram (xx-$shift) (hratio_with_ll-$shift)
	ctype 4 histogram (xx+$shift) (hratio_good+$shift)

	ctype 7 
	ltype 2
	relocate $r_20 $fy1 draw $r_20 $fy2 
	relocate $r_25 $fy1 draw $r_25 $fy2 
	relocate $r_30 $fy1 draw $r_30 $fy2 
	ctype 2 
	ltype 0

	echo "---------------------------------------------"
	echo " black crosses :  upper limit / upper limit  ===>  n/a "
	echo " red   squares :         GOOD / upper limit  ===> LOWER limit "
	echo " cyan  squares :  upper limit / GOOD         ===> UPPER limit  "
	echo " green squares :         GOOD / GOOD         ===>  GOOD"
	echo "---------------------------------------------"

	relocate ( $(0.5*($gx1+$gx2)) $($gy2 + 750) )
	putlabel 6 $9

##----------------------------------------------------------
## compute_ratio_ab <band_a> <band_b> <Gamma> <output var name>
compute_ratio_ab 4

	#--- preset values ---#
	define x1a (0.1)
	define x2a (300.0)
	define x1b (0.3)
	define x2b (1.0)
	define x1c (1.0)
	define x2c (300.0)

	foreach 9 < 11 12 21 22 > {
	   define x$9 local
	}
	define xa1 $x1$1
	define xa2 $x2$1
	define xb1 $x1$2
	define xb2 $x2$2

	define fa local
	define fb local
	define ratio local

	define fa $( $xa1**(1.0-$3)/($3-1.0)*(1.0 - ($xa1/$xa2)**($3-1.0)) )
	define fb $( $xb1**(1.0-$3)/($3-1.0)*(1.0 - ($xb1/$xb2)**($3-1.0)) )
	define ratio $($fa/$fb)

	echo $fa $fb "==>" $ratio

	define $4 $ratio

#===============================================================================
# read_smarts_and_uvot_3c454
read_smarts_and_uvot_3c454

	foreach 9 < b v r j > {
	   #--- "arbitrary" units ---#
	   data smarts_$9l.dat
	   read < smarts_$9_jd 1 smarts_$9_f 2 smarts_$9_ef 3 >
	   set smarts_$9_tjd = smarts_$9_jd - 2400000.5

	   set smarts_$9_lgf   =    lg(smarts_$9_f)
	   set smarts_$9_eulgf =    lg(1 + smarts_$9_ef/smarts_$9_f)
	   set smarts_$9_edlgf = -1*lg(1 - smarts_$9_ef/smarts_$9_f)
	
	   #--- flux units values ---#
	   data smarts_$9_vFv.dat
	   read < smarts_f_$9_jd 1 smarts_f_$9_f 2 smarts_f_$9_ef 3 >
	   set smarts_f_$9_tjd = smarts_f_$9_jd - 2400000.5

	   set smarts_f_$9_lgf   =    lg(smarts_f_$9_f)
	   set smarts_f_$9_eulgf =    lg(1 + smarts_f_$9_ef/smarts_f_$9_f)
	   set smarts_f_$9_edlgf = -1*lg(1 - smarts_f_$9_ef/smarts_f_$9_f)
	}

	foreach 9 < u b v > { 
	   #--- "arbitrary" units ---#
	   data uvot_$9$9.dat
	   read < uvot_$9_jd 1 uvot_$9_f 2 uvot_$9_ef 3 >
	   set uvot_$9_tjd = uvot_$9_jd - 2400000.5

	   set uvot_$9_lgf   =    lg(uvot_$9_f)
	   set uvot_$9_eulgf =    lg(1 + uvot_$9_ef/uvot_$9_f)
	   set uvot_$9_edlgf = -1*lg(1 - uvot_$9_ef/uvot_$9_f)
	
	   #--- flux units values ---#
	   data uvot_$9$9_vFv.dat
	   read < uvot_f_$9_jd 1 uvot_f_$9_f 2 uvot_f_$9_ef 3 >
	   set uvot_f_$9_tjd = uvot_f_$9_jd - 2400000.5

	   set uvot_f_$9_lgf   =    lg(uvot_f_$9_f)
	   set uvot_f_$9_eulgf =    lg(1 + uvot_f_$9_ef/uvot_f_$9_f)
	   set uvot_f_$9_edlgf = -1*lg(1 - uvot_f_$9_ef/uvot_f_$9_f)
	}

	foreach 9 < b v > { 
	   #--- "arbitrary" units ---#
	   set all_$9_tjd = smarts_$9_tjd CONCAT uvot_$9_tjd
	   set all_$9_f   = smarts_$9_f   CONCAT uvot_$9_f
	   set all_$9_ef  = smarts_$9_ef  CONCAT uvot_$9_ef

	   sort < all_$9_tjd all_$9_f all_$9_ef >

	   set all_$9_lgf   = lg(all_$9_f)
	   set all_$9_eulgf = lg(1 + all_$9_ef/all_$9_f)
	   set all_$9_edlgf = -1*lg(1 - all_$9_ef/all_$9_f)
	
	   #--- flux units values ---#
	   set all_f_$9_tjd = smarts_f_$9_tjd CONCAT uvot_f_$9_tjd
	   set all_f_$9_f   = smarts_f_$9_f   CONCAT uvot_f_$9_f
	   set all_f_$9_ef  = smarts_f_$9_ef  CONCAT uvot_f_$9_ef

	   sort < all_f_$9_tjd all_f_$9_f all_f_$9_ef >

	   set all_f_$9_lgf   = lg(all_f_$9_f)
	   set all_f_$9_eulgf = lg(1 + all_f_$9_ef/all_f_$9_f)
	   set all_f_$9_edlgf = -1*lg(1 - all_f_$9_ef/all_f_$9_f)
	}

	echo "------------------------------------------------------------"
	echo " SMARTS: b v r j "
	echo " UVOT  : u b v "
	echo " BOTH  : b v "
	echo "------------------------------------------------------------"

#-------------------------------------------------------------------------------
# plot_smarts_and_uvot_3c454
plot_smarts_and_uvot_3c454

	reset_graph

	define whichband ? < Which band [b|v] : >
	define 9 $whichband

	notation -4 5 -4 4 
	limits all_$9_tjd all_$9_lgf
	limits all_$9_tjd -6.8 -5.8
	limits 54675 54820 -6.8 -5.8

	box
	xlabel Time [TJD]
	ylabel "mag" ($9)

	ptype 4 3 
	ctype 3 points uvot_$9_tjd   uvot_$9_lgf
	ctype 5 points smarts_$9_tjd smarts_$9_lgf
	ctype 2 

#-------------------------------------------------------------------------------
# plot_smarts_and_uvot_f_3c454
plot_smarts_and_uvot_f_3c454

	reset_graph

	define whichband ? < Which band [b|v] : >
	define 9 $whichband

	notation -4 5 -4 4 
	# limits all_f_$9_tjd -6.8 -5.8
	limits all_f_$9_tjd all_f_$9_lgf
	limits 54675 54820 -11.2 -10.4
	limits all_f_$9_tjd -11.2 -10.4

	box
	xlabel Time [TJD]
	ylabel \\nu F_\\nu ($9)

	ptype 4 3 
	ctype 3 points uvot_f_$9_tjd   uvot_f_$9_lgf
	ctype 5 points smarts_f_$9_tjd smarts_f_$9_lgf
	ctype 2 

#---------------------------------------------------------------------------
# smarts_fvar
smarts_fvar

	echo "-------------------------------------------------------------"
	define end ? < Prefix of arrays [e.g. smarts_j]: >
	define 1 $end

	define t1 ? < Start Time : >
	define t2 ? < End   Time : >

	set time    local
	set test_y  local
	set sigma_y local
	set test_w  local

	set time   = $1_tjd
	set test_y = $1_f     if(time>$t1 && time<$t2 && $1_f>0)
	set test_w = $1_ef    if(time>$t1 && time<$t2 && $1_f>0)
	set sigma_y = test_w

	mystats test_y mean_y  width_y  absdev_y skew_y
	median  test_y med

	define N local
	define N (dimen(test_y))

	echo "-----------------------------------------------------------------"
	echo "* DATA:" $end 
	echo "*       N =" $(dimen(test_y))
	echo "*"
	echo "* Average Flux    =" $mean_y   
	echo "* sigma           =" $width_y  
	echo "* Skewness        =" $skew_y   
	# echo "* Median          =" $med

	#-----------------------------------------------------------
	# "new" method, based on defining an array whose average
	# represents sigma^2_rms (=F_var^2), and whose variance is the related
	# to the uncertainty on F_var
	#
	echo "-----------------------------------------------------------------"
	echo "*** Alternative (array based) computation: "

	set sigma2_rms = ((test_y - $mean_y)**2.0 - sigma_y**2.0)/($mean_y**2.0)
	mystats sigma2_rms s2r_mean s2r_sigma dum1 dum2

	echo "  Average sigma2  =" $s2r_mean 
	echo "  sigma(sigma2)   =" $s2r_sigma
	echo "  err(sigma2)     =" $($s2r_sigma/sqrt($N))

	define fvar_alt     $( sqrt($s2r_mean) )
	define s2r_err      $( $s2r_sigma/sqrt($N) )
        define err_fvar_alt $( ($s2r_err/2.0/$fvar_alt) )
	echo "==> Alt F_var : " $fvar_alt "+/-" $err_fvar_alt

	#-----------------------------------------------------------
	# "old" method, by defining separate pieces as variables.
	# Origin of expression for error on fvar UNKNOWN (?)
	#
	define sigma_f2 ( sum((test_y - $mean_y)**2.)/$N )
	define delta_f2 ( sum(test_w*test_w)/$N )

	define fvar ( sqrt($sigma_f2 - $delta_f2)/$mean_y )
	define err_fvar ($fvar*($sigma_f2/$delta_f2)*sqrt(2/$(dimen(test_y)-1))/2/($sigma_f2/$delta_f2-1) )

	define err_fvar_vaughan ( sqrt( ( sqrt(1./2./$N)*$delta_f2/$mean_y/$fvar )**2. + ( sqrt($delta_f2/$N)/$mean_y )**2. ) )

	# echo "-----------------------------------------------------------------"
	# echo "* Sigma_F =" $(sqrt($sigma_f2))      
	# echo "* Delta_F =" $(sqrt($delta_f2))
	echo "-----------------------------------------------------------------"
	echo "*   F_Var =" $fvar "+/-" $err_fvar 
	echo "                       alt: "$err_fvar_alt "[new/old : "$($err_fvar_alt/$err_fvar)"]"
	echo "                   vaughan: "$err_fvar_vaughan
	echo "-----------------------------------------------------------------"

#-------------------------------------------------------------------------------
# print_smarts_uvot_3c454
print_smarts_uvot_3c454

	set dummy=1,dimen(all_b_tjd)
	print smarts_uvot_b.dat    ' %4d  %12.5f  %4d  %10.4e  %10.4e\n' < dummy all_b_tjd dummy all_b_f all_b_ef >

	set dummy=1,dimen(all_v_tjd)
	print smarts_uvot_v.dat    ' %4d  %12.5f  %4d  %10.4e  %10.4e\n' < dummy all_v_tjd dummy all_v_f all_v_ef >
	# print smarts_uvot_b_lg.dat ' %4d  %12.5f  %4d  %10.4f  %10.4f\n' < dummy all_b_tjd dummy all_b_lgf all_b_ef >
	# print smarts_uvot_v_lg.dat ' %4d  %12.5f  %4d  %10.4f  %10.4f\n' < dummy all_v_tjd dummy all_v_lgf all_v_ef >

	set dummy=1,dimen(all_f_b_tjd)
	print smarts_uvot_b_vFv.dat    ' %4d  %12.5f  %4d  %10.4e  %10.4e\n' < dummy all_f_b_tjd dummy all_f_b_f all_f_b_ef >

	set dummy=1,dimen(all_f_v_tjd)
	print smarts_uvot_v_vFv.dat    ' %4d  %12.5f  %4d  %10.4e  %10.4e\n' < dummy all_f_v_tjd dummy all_f_v_f all_f_v_ef >

#===============================================================================
